import pokemon.*
import estadio.*

describe "Evolución de pokemones" {
	const charmander = new Pokemon(hambre = 0, especie = new Charmander())
	const charmeleon = new Pokemon(hambre = 0, especie = new Charmeleon())
	
	test "Un charmander puede evolucionar cuando tiene al menos 5 de felicidad" {
		estadio.temperatura(30)
		assert.that(charmander.puedeEvolucionar())
	}
	test "Un charmander no puede evolucionar cuando tiene menos de 5 de felicidad" {
		estadio.temperatura(1)
		assert.notThat(charmander.puedeEvolucionar())
	}
	
	test "La alegría de un charmeleon es 5 más la mitad de la temperatura" {
		estadio.temperatura(10)
		assert.equals(10, charmeleon.alegria())
	}
	
	test "Un charmeleon no puede evolucionar aunque tenga más de 5 de felicidad" {
		estadio.temperatura(10)
		assert.notThat(charmeleon.puedeEvolucionar())
	}
	
	test "Cuando un pokemon evoluciona, su especie cambia"{
		estadio.temperatura(30)
		const especieInicial = charmander.especie()
		charmander.evolucionar()
		assert.notEquals(especieInicial, charmander.especie())
	}
	test "Cuando un pokemon evoluciona, mantiene su nivel de hambre"{
		estadio.temperatura(30)
		charmander.hambre(5)
		charmander.evolucionar()
		assert.equals(5, charmander.hambre())
	}
	
	test "Cuando se intenta evolucionar un pokemon que puede hacerlo, se lanza una excepción"{
		assert.throwsException {charmeleon.evolucionar()}
	}
}