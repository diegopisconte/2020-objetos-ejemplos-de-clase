import clase07.estudiante.*

describe "Estudiante" {
	method estudianteConcentradoConHorasDeEstudio(unasHorasDeEstudio) =
		new Estudiante(energia=51, estresado=false, horasDeEstudio=unasHorasDeEstudio)
	method estudianteDesconcentrado() = new Estudiante(energia=0)
	
	test "su nivel de concentracion cuando no esta estresado es su energia" {
		const estudianteDesestresado = new Estudiante(energia=30, estresado=false)
		
		assert.equals(30, estudianteDesestresado.concentracion())
	}

	test "su nivel de concentracion cuando esta estresado es la mitad de su energia" {
		const estudianteEstresado = new Estudiante(energia=30, estresado=true)
		
		assert.equals(15, estudianteEstresado.concentracion())
	}
	
	test "esta concentrado si su nivel de concentracion es mayor a 50" {
		const estudianteConcentrado = new Estudiante(energia=51, estresado=false)

		assert.that(estudianteConcentrado.concentrado())
	}
	
	test "no esta concentrado si su nivel de concentracion es igual a 50" {
		const estudianteConcentrado = new Estudiante(energia=50, estresado=false)

		assert.notThat(estudianteConcentrado.concentrado())
	}
	
	test "no esta concentrado si su nivel de concentracion es menor a 50" {
		const estudianteDesconcentrado = new Estudiante(energia=49, estresado=false)

		assert.notThat(estudianteDesconcentrado.concentrado())
	}
	
	test "no esta preparado si estudio menos de 40 horas" {
		const estudianteNoPreparado = self.estudianteConcentradoConHorasDeEstudio(39)
		
		assert.notThat(estudianteNoPreparado.preparado())
	}
	
	test "no esta preparado si estudio 40 horas" {
		const estudiantePreparado = self.estudianteConcentradoConHorasDeEstudio(40)
		
		assert.notThat(estudiantePreparado.preparado())
	}
	
	test "no esta preparado si no esta concentrado" {
		const estudianteNoPreparado = self.estudianteDesconcentrado()
		
		assert.notThat(estudianteNoPreparado.preparado())
	}
	
	test "esta preparado si esta concentrado y estudio mas de 40 horas" {
		const estudiantePreparado = self.estudianteConcentradoConHorasDeEstudio(41)
		
		assert.that(estudiantePreparado.preparado())
	}
}
